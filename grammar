# The mc grammar spec

# Note on the relational operators:
# These operators are chained non-accumulatively,
# meaning 2 > 1 > 0 is evaluated as 2 > 1 AND 1 > 0.

<sentence> ::= <equality>
	| <sentence> "||" <equality>
	| <sentence> "&&" <equality>

<equality> ::= <relational>
	| <equality> "==" <relational>
	| <equality> "!=" <relational>

<relational> ::= <calculate>
	| <relational> ">" <calculate>
	| <relational> "<" <calculate>
	| <relational> ">=" <calculate>
	| <relational> "<=" <calculate>

<calculate> ::= <operation> | <expression>

<shift> ::= <bitwise>
	| <shift> ">>" <bitwise>
	| <shift> "<<" <bitwise>

<bitwise> ::= <additive>
	| <bitwise> "|" <additive>
	| <bitwise> "&" <additive>
	| <bitwise> "^" <additive>

<additive> ::= <multiplicative>
	| <additive> "+" <multiplicative>
	| <additive> "-" <multiplicative>

<multiplicative> ::= <primary>
	| <multiplicative> "*" <primary>
	| <multiplicative> "**" <primary>
	| <multiplicative> "/" <primary>
	| <multiplicative> "%" <primary>
	| <multiplicative> "!"

<primary> ::= <number>
	| <paren>
	| <unary>
	| <function>
	| <constant>

<paren> ::= "(" <sentence> ")"

<unary>	::= "+" <primary>
	| "-" <primary>
	| "~" <primary>

<constant> ::= "pi" | "e"

<function> ::= "sin" <paren>
	| "cos" <paren>
	| "tan" <paren>
	| "asin" <paren>
	| "acos" <paren>
	| "atan" <paren>
	| "root" <primary> <paren>
	| "ln" <paren>
	| "abs" <paren>
	| "log" <paren>
	| "log" <primary> <paren>
